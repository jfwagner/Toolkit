//**********************
// COLLISION FOR HL-LHC
//**********************
option, warn,info; 

//-----------------------------------------
// Shortcut creation for needed directories
//-----------------------------------------
system,"ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.0 slhc";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";
system,"ln -fns /afs/cern.ch/user/a/ansantam/public/toolkit tkit";

//------------
// Verbose off
//------------
option, -echo,-warn,-info; 

//--------------------------
// Call macro for ATS optics
//--------------------------
call,file="slhc/toolkit/macro.madx";

!on_disp=1; !parasitic dispersion

mylhcbeam=1;! For Beam 1 (and 2)
!mylhcbeam=2;! For Beam 2 (and 1)
!mylhcbeam=4;! For Beam 4

//-------------
// Round Optics
//-------------
call,file="slhc/opt_round.madx";

//---------------------------
// Avoid some negative drifts
//---------------------------
REAL CONST l.TAN   = 3.7  ;REAL CONST l.TANAL = l.TAN;
REAL CONST l.TANAR = l.TAN;REAL CONST l.TANC  = l.TAN;
REAL CONST l.TCT   = 1.0;REAL CONST l.TCTH  = l.TCT;REAL CONST l.TCTVA = l.TCT;
if (mylhcbeam>2){
  call,file="db5/beam_four.seq";
} else {
  call,file="db5/V6.5.seq";
};

Option, -echo,warn,-info,no_fatal_stop;

//----------------------------------------
// Load HL sequence (position of elements)
//----------------------------------------
call,file="slhc/hllhc_sequence.madx";

//----------------------------------
// Load and define the aperture (HL)
//----------------------------------
call,   file="db5/aperture/aperture.b1.madx";
call,   file="db5/aperture/aper_tol.b1.madx";
call,   file="db5/aperture/aperture.b2.madx";
call,   file="db5/aperture/aper_tol.b2.madx";
bs_type=5; ap_mqx=150;
call,   file="slhc/aperture/aperture_upgrade_IT.madx";
call,   file="slhc/aperture/aperture_upgrade_MS.madx";

/*
seqedit,sequence=lhcb1; flatten; cycle,start=s.ds.l8.b1; endedit;
seqedit,sequence=lhcb2; flatten; cycle,start=s.ds.l8.b2; endedit;

if(NRJ <6999.99) { COmax:=0.004; dPmax=0.00150; };
if(NRJ >6999.99) { COmax:=0.003; dPmax=0.00086; };
emittance_norm = 3.75e-06 ; apbbeat = 1.1 ;
halor = 8.4 ; halox = 7.3 ; haloy = 7.3 ;
*/

//------------
//Verbose off
//------------
option, -echo,-warn,-info; 

exec,crossing_enable;
exec,mk_beam(7000);

//-------------------------------------
//Load the beam parameters (sigmas etc)
//-------------------------------------
call,file="tkit/Beam_7_TeV.madx";  !Important to call these files first, if not Beam.madx doesnt work
!call,file="tkit/Beam_450_GeV.madx";
call,file="tkit/Beam.madx";   

//-----------------------------------------
//Crossing angle, separation, spectrometers
//-----------------------------------------
on_x1:=1.; on_sep1:=0;
on_x2:=1; on_sep2:=0; on_alice:=1;
on_x5:=1.; on_sep5:=0;
on_x8:=1; on_sep8:=0; on_lhcb:=1;

/*
exec,mk_irtwiss(5,b1); exec,mk_irtwiss(5,b2);
!exec,crossing_disable;full=0;
exec,crossing_enable;full=1;
*/

//---------------------------------------------------------------------------------------------------
// PARAMETERS FILE
//---------------------------------------------------------------------------------------------------
//*******
// BEAM 1
//*******
//-----------------------------------
// Use the sequence you want (beam 1)
//-----------------------------------
use,sequence=lhcb1;

//-------------------------------------
// Define the type and amount of output
//-------------------------------------
select,flag=twiss,clear;
select,flag=twiss,
column=name,s,l,betx,bety,sigx,ex,sigy,ey,dx,dpx,dy,dpy,x,y,aper_1,aper_2,aper_3,aper_4,alfx,alfy,px,py,sigxd,sigyd,wx,wy,T,PT,DELTAP,beta;

//---------------------------------------------------------------------------------------------------
// Execute the Twiss command to calculate the Twiss parameters and save the data in columns in .tfs
//---------------------------------------------------------------------------------------------------
twiss,table=twiss,file="param_b1_ip5.tfs";

//*******
//BEAM 2  
//*******
//-----------------------------------
//Use the sequence you want (beam 2)
//-----------------------------------
use,sequence=lhcb2;

//-------------------------------------
//Define the type and amount of output
//-------------------------------------
select,flag=twiss,clear;
select,flag=twiss,
column=name,s,l,betx,bety,sigx,ex,sigy,ey,dx,dpx,dy,dpy,x,y,aper_1,aper_2,aper_3,aper_4,alfx,alfy,px,py,sigxd,sigyd,wx,wy,T,PT,DELTAP,beta;

//---------------------------------------------------------------------------------------------------
// Execute the Twiss command to calculate the Twiss parameters and display the data in columns in .tfs
//---------------------------------------------------------------------------------------------------
twiss,table=twiss,file="param_b2_ip5.tfs";

//---------------------------------------------------------------------------------------------------
// TWISS FILE
//---------------------------------------------------------------------------------------------------
//****************
// To start at IP5
//****************
seqedit,sequence=lhcb1; flatten; cycle,start=IP5; endedit;
seqedit,sequence=lhcb2; flatten; cycle,start=IP5; endedit;

//*******
// BEAM 1
//*******
//-----------------------------------
// Use the sequence you want (beam 1)
//-----------------------------------
use,sequence=lhcb1;

//-------------------------------------
// Define the type and amount of output
//-------------------------------------
select,flag=twiss,clear;
select,flag=twiss,
column=name,s,l,betx,bety,sigx,ex,sigy,ey,dx,dpx,dy,dpy,x,y,aper_1,aper_2,aper_3,aper_4,alfx,alfy,px,py,sigxd,sigyd,wx,wy,T,PT,DELTAP,beta;

//---------------------------------------------------------------------------------------------------
// Execute the Twiss command to calculate the Twiss parameters and save the data in columns in .tfs
//---------------------------------------------------------------------------------------------------
twiss,table=twiss,file="twiss_ip5_b1.tfs";

//------------------------------------
// Generate a file with apertures only
//------------------------------------
select,flag=twiss,clear;
use,sequence=lhcb1;
select, flag=twiss, pattern =full ,column=name,s,l,aper_1,aper_2,aper_3,aper_4,apertype,x,y,z;
twiss,table=twiss,file="aperture_ip5_b1.tfs";

// Show half x separation 
value,table(twiss,IP5,x); 
// Show half y separation  
value,table(twiss,IP5,y); 
// Show half crossing x 
value,table(twiss,IP5,px); 
// Show half crossing y
value,table(twiss,IP5,py);
// Show beta x 
value,table(twiss,IP5,betx); 
// Show beta y
value,table(twiss,IP5,bety); 
// Show s
value,table(twiss,IP5,s);

//*******
// BEAM 2  
//*******
//-----------------------------------
// Use the sequence you want (beam 2)
//-----------------------------------
use,sequence=lhcb2;

//-------------------------------------
// Define the type and amount of output
//-------------------------------------
select,flag=twiss,clear;
select,flag=twiss,
column=name,s,l,betx,bety,sigx,ex,sigy,ey,dx,dpx,dy,dpy,x,y,aper_1,aper_2,aper_3,aper_4,alfx,alfy,px,py,sigxd,sigyd,wx,wy,T,PT,DELTAP,beta;

//---------------------------------------------------------------------------------------------------
// Execute the Twiss command to calculate the Twiss parameters and display the data in columns in .tfs
//---------------------------------------------------------------------------------------------------
twiss,table=twiss,file="twiss_ip5_b2.tfs";

//------------------------------------
// Generate a file with apertures only
//------------------------------------
select,flag=twiss,clear;
use,sequence=lhcb2;
select, flag=twiss, pattern =full ,column=name,s,l,aper_1,aper_2,aper_3,aper_4,apertype,x,y,z;
twiss,table=twiss,file="aperture_ip5_b2.tfs";


// Show half x separation 
value,table(twiss,IP5,x); 
// Show half y separation  
value,table(twiss,IP5,y); 
// Show half crossing x 
value,table(twiss,IP5,px); 
// Show half crossing y
value,table(twiss,IP5,py);
// Show beta x 
value,table(twiss,IP5,betx); 
// Show beta y
value,table(twiss,IP5,bety); 
// Show s
value,table(twiss,IP5,s);

//---------------------------------------------------------------------------------------------------
// SURVEY FILE
//---------------------------------------------------------------------------------------------------

if (mylhcbeam<=2){
  exec,check_ip(b1); survey,file="survey_ip5_b1.tfs";
  exec,check_ip(b2); survey,file="survey_ip5_b2.tfs";
} else {
  exec,check_ip(b2); survey,file="survey_ip5_b4.tfs";
};

stop;




